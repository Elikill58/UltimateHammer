import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['testSrc']
        resources.srcDirs = ['testResources']
    }
}

eclipse {
    project {
        name = 'UHv2-common'
    }
}

dependencies {
    implementation "org.yaml:snakeyaml:1.33"
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching(['META-INF/services/*']) {
        expand(props)
    }
}

shadowJar {
    archiveBaseName.set('ultimatehammer')
    archiveAppendix.set('common')
    archiveClassifier.set(null)
    
    relocate("org.yaml.snakeyaml", "com.elikill58.deps.yaml")
    minimize {
        include(dependency("it.unimi.dsi:fastutil:.*"))
        include(dependency("org.yaml:snakeyaml:.*"))
    }
    
    mergeServiceFiles()
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}